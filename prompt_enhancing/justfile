set dotenv-load

DEFAULT_INPUT_DIR := "./inputs"
DEFAULT_EXP_NAME := "main"

default:
  @just --list

# poetry must be installed
[group("install")]
install:
  poetry install

# suppose this shell is in the environment of your jupyter stack
[group("install")]
[group("notebook")]
init-nb-git-workspace:
  # Install the required dependencies for running notebooks
  poetry install --all-extras
  # Install a hook in the git repo so you only track notebooks source content
  # without the cells' outputs
  cd .. && nbstripout --install && \
    nbdime config-git --enable

# suppose this shell is in the environment of your jupyter stack
[group("notebook")]
run-notebooks:
  jupyter --no-browser notebook notebooks/

# suppose this shell is in the environment of your jupyter stack
[group("notebook")]
run-lab:
  jupyter --no-browser lab notebooks/

# suppose the dev deps has been installed
[group("dev")]
dev-docs:
  poetry run mkdocs serve

[group("monitor")]
try_pipeline:
  poetry run python ./src/try_dataload.py

[group("test")]
test:
  poetry run pytest tests/

[group("monitor")]
serve-tracer:
  poetry run mlflow server --host $MLFLOW_HOST --port $MLFLOW_PORT

[group("test")]
type_check:
  poetry run mypy src
