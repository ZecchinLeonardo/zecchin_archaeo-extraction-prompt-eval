set dotenv-load

DEFAULT_INPUT_DIR := "./inputs"
DEFAULT_EXP_NAME := "main"

default:
  @just --list

# poetry must be installed
[group("install")]
install:
  poetry install

# suppose this shell is in the environment of your jupyter stack
[group("install")]
[group("notebook")]
init-nb-git-workspace:
  # Install the required dependencies for running notebooks
  poetry install --all-extras
  # Install a hook in the git repo so you only track notebooks source content
  # without the cells' outputs
  cd .. && nbstripout --install && \
    nbdime config-git --enable

# suppose this shell is in the environment of your jupyter stack
[group("notebook")]
run-notebooks:
  jupyter notebook notebooks/

# suppose this shell is in the environment of your jupyter stack
[group("notebook")]
run-lab:
  jupyter lab notebooks/

[group("main")]
run_main EXP_NAME=DEFAULT_EXP_NAME INPUT_DIR=DEFAULT_INPUT_DIR:
  poetry run main --experiment-name {{EXP_NAME}} --report-dir {{INPUT_DIR}}

[group("monitor")]
try_pipeline:
  poetry run python ./src/try_dataload.py

[group("test")]
test:
  poetry run python tests/magoh_transform.py

[group("monitor")]
serve-tracer:
  poetry run mlflow server --host $MLFLOW_HOST --port $MLFLOW_PORT

[group("test")]
type_check:
  poetry run mypy src
